apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode "$PROJECT_VERSION_CODE" as int
        versionName "$PROJECT_VERSION_NAME"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

apply plugin: 'maven'
uploadArchives {

    def username = ""
    def password = ""

    if (new File('local.properties').exists()) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        username = properties.getProperty("maven.username")
        password = properties.getProperty("maven.password")
    }

    repositories {
        mavenDeployer {

            snapshotRepository(url: 'http://maven.qima-inc.com/content/repositories/snapshots/') {
                authentication(userName: username, password: password)
            }

            repository(url: 'http://maven.qima-inc.com/content/repositories/releases/') {
                authentication(userName: username, password: password)
            }

            pom.packaging = 'aar'
            pom.version = "$PROJECT_VERSION_NAME"
            pom.groupId = 'com.youzan.mobile'
            pom.artifactId = "genesis"
        }
    }
}


configurations {
    javadocDeps
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += configurations.javadocDeps
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'

    compile 'com.youzan.mobile:zanpermissions:1.1.2'
}
